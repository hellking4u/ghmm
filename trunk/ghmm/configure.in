dnl  author       : Achim Gaedke
dnl  filename     : ghmm/configure.in
dnl  created      : DATE: 2001-04-04
dnl  $Id$
dnl __copyright__

dnl Process this file with autoconf to produce a configure script.
AC_INIT(ghmm/smodel.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(ghmm,0.3)
dnl implicit in AM_INIT_AUTOMAKE...
dnl AC_PROG_MAKE_SET

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_RANLIB
AC_PROG_AWK
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(DOCXX,doc++)
AC_PATH_PROG(LATEX,latex)
AC_PATH_PROG(DVIPS,dvips)

AC_PATH_PROG(EXPECT_FOR_DEJAGNU,expect)
AC_PATH_PROG(RUNTEST_FOR_DEJAGNU,runtest)

dnl experimental things can be switched on or off
AC_ARG_ENABLE(experimental,[  --enable-experimental   enable experimental features],
if test "x$enable_experimental" != "xno" ; then
AC_DEFINE_UNQUOTED(__EXPERIMENTAL__,$enable_experimental,include experimental features)
fi
)

dnl these macros are from acinclude.m4! (modified gsl.m4)
AC_LANG_CPLUSPLUS
AC_CXX_HAVE_STL
AC_CXX_NAMESPACES

AC_ARG_WITH(expat,
[  --with-expat=DIR        with expat available at the given prefix],
[CPPFLAGS="$CPPFLAGS -I$with_expat/include"
LIBS="$LIBS -L$with_expat/lib"]
)
AC_CHECK_HEADERS(expat.h)

AC_CHECK_LIB(expat,XML_ParserCreate)

AC_ARG_WITH(xmlio,
[  --with-xmlio=DIR        with xmlio library available at the given prefix],
[
XMLIO_PREFIX="$with_xmlio"
CXXFLAGS="$CXXFLAGS -I$XMLIO_PREFIX/include"
LIBS="$LIBS -L$XMLIO_PREFIX/lib"
AC_SUBST(XMLIO_PREFIX)]
)

dnl following tests with C
AC_LANG_C
dnl aborts on failure
AM_PATH_GSL(0.7,,AC_MSG_ERROR("GSL Version 0.7 or higher required."))
LIBS="$LIBS $GSL_LIBS"
CFLAGS="$CFLAGS $GSL_CFLAGS"
CXXFLAGS="$CXXFLAGS $GSL_CFLAGS"

dnl Checks for libraries.
AC_CHECK_LIB(pthread, pthread_join)

dnl something experimental
dnl if test "x$enable_experimental" = "x1" || test "x$enable_experimental" = "xyes" ; then
dnl evaluate experimental flags
dnl fi

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

AC_HEADER_TIME
AC_STRUCT_TM

dnl AC_CHECK_FUNCS(tmpnam mkstemp)
dnl AC_CHECK_FUNCS(gettimeofday)

dnl checking which root solver alloc function is used
AC_MSG_CHECKING([whether gsl_root_fsolver_alloc takes one argument])
AC_TRY_COMPILE([#include <gsl/gsl_roots.h>],
[
 gsl_root_fsolver* s;
 s = gsl_root_fsolver_alloc (gsl_root_fsolver_bisection);
],
AC_MSG_RESULT(yes)
AC_DEFINE(GSL_ROOT_FSLOVER_ALLOC_WITH_ONE_ARG,1,[root solver allocation takes only one argument]),
AC_MSG_RESULT(no)
)

dnl checking for gsl_histogram_set_ranges_uniform
AC_MSG_CHECKING([for gsl_histogram_set_ranges_uniform])
AC_TRY_LINK([#include <gsl/gsl_histogram.h>],
[
 gsl_histogram* h;
 (void)gsl_histogram_set_ranges_uniform(h,-10.0,10.0);
],
AC_MSG_RESULT(yes)
AC_DEFINE(GSL_HISTOGRAM_SET_RANGES_UNIFORM,1,[gsl_histogram_set_ranges_uniform is defined]),
AC_MSG_RESULT(no)
)

dnl checking if gsl_sf_erf exists
AC_MSG_CHECKING([for gsl_sf_erf])
AC_TRY_LINK([#include <gsl/gsl_sf.h>],
[
 (void)gsl_sf_erf(0.0);
],
AC_MSG_RESULT(yes)
AC_DEFINE(HAVE_GSL_SF_ERF,1,[gsl_sf_erf exists]),
AC_MSG_RESULT(no)
)

dnl checking if gsl_sf_erfc exists
AC_MSG_CHECKING([for gsl_sf_erfc])
AC_TRY_LINK([#include <gsl/gsl_sf.h>],
[
 (void)gsl_sf_erfc(0.0);
],
AC_MSG_RESULT(yes)
AC_DEFINE(HAVE_GSL_SF_ERFC,1,[gsl_sf_erfc exists]),
AC_MSG_RESULT(no)
)

# substitute default prefix here, because I need it now 
test "x$prefix" = xNONE && prefix=$ac_default_prefix
phi_data_file=`eval echo ${datadir}`/${PACKAGE}/PHI_001_20.dat
AC_DEFINE_UNQUOTED(PHI_DATA_FILE,"${phi_data_file}")
AC_DEFINE(DO_WITH_GSL,1,[use GSL functions instead of ghmm interpolation algorithms])

SUBDIRS="ghmm tools tests doc"
if test "$ac_cv_cxx_have_stl" = yes && test -n "$with_xmlio" ; then
 SUBDIRS="$SUBDIRS ghmm++"
fi
AC_SUBST(SUBDIRS)

AC_OUTPUT(Makefile doc/Makefile ghmm/Makefile tools/Makefile tests/Makefile tests/data/Makefile doc/docu.dxx tools/ghmm-config ghmm++/Makefile)

