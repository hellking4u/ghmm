dnl  author       : Achim Gaedke
dnl  filename     : ghmm/configure.in
dnl  created      : DATE: 2001-04-04
dnl  $Id$
dnl __copyright__

dnl Process this file with autoconf to produce a configure script.
AC_INIT(ghmm/smodel.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(ghmm,0.3)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_RANLIB
AC_PROG_AWK
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(DOCXX,doc++)
AC_PATH_PROG(LATEX,latex)
AC_PATH_PROG(DVIPS,dvips)

AC_PATH_PROG(EXPECT_FOR_DEJAGNU,expect)
AC_PATH_PROG(RUNTEST_FOR_DEJAGNU,runtest)

dnl experimental things can be switched on or off
AC_ARG_ENABLE(experimental,[  --enable-experimental   enable experimental features],
if test "x$enable_experimental" != "xno" ; then
AC_DEFINE_UNQUOTED(__EXPERIMENTAL__,$enable_experimental,include experimental features)
fi
)

dnl this macro is in acinclude.m4! (modified gsl.m4)
dnl aborts on failure
AM_PATH_GSL(0.7,,AC_MSG_ERROR("GSL Version 0.7 or higher required."))
LIBS="$LIBS $GSL_LIBS"
CFLAGS="$CFLAGS $GSL_CFLAGS"

dnl Checks for libraries.
AC_CHECK_LIB(pthread, pthread_join)
if test "x$enable_experimental" = "x1" || test "x$enable_experimental" = "xyes" ; then
  AC_CHECK_LIB(expat, XML_ParserCreate)
fi

dnl Checks for header files.
AC_HEADER_STDC
if test "x$enable_experimental" = "x1" || test "x$enable_experimental" = "xyes" ; then
  AC_CHECK_HEADERS(expat.h)
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

AC_HEADER_TIME
AC_STRUCT_TM

dnl AC_CHECK_FUNCS(tmpnam mkstemp)
dnl AC_CHECK_FUNCS(gettimeofday)

dnl checking which root solver alloc function is used
AC_MSG_CHECKING([whether gsl_root_fsolver_alloc takes one argument])
AC_TRY_COMPILE([#include <gsl/gsl_roots.h>],
[
 gsl_root_fsolver* s;
 s = gsl_root_fsolver_alloc (gsl_root_fsolver_bisection);
],
AC_MSG_RESULT(yes)
AC_DEFINE(GSL_ROOT_FSLOVER_ALLOC_WITH_ONE_ARG,1,[root solver allocation takes only one argument]),
AC_MSG_RESULT(no)
)

AC_OUTPUT(Makefile doc/Makefile ghmm/Makefile tools/Makefile tests/Makefile doc/docu.dxx tools/ghmm-config)






